apply plugin: 'com.android.application'

android {
    compileSdkVersion project.COMPILE_SDK_VERSION
    defaultConfig {
        applicationId project.SAMPLE_HOST_APP_APPLICATION_ID
        minSdkVersion project.MIN_SDK_VERSION
        targetSdkVersion project.TARGET_SDK_VERSION
        versionCode project.VERSION_CODE
        versionName project.VERSION_NAME
        testInstrumentationRunner "com.tencent.shadow.test.CustomAndroidJUnitRunner"
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.create("release")
            signingConfig.initWith(buildTypes.debug.signingConfig)
        }
    }
    sourceSets {
        debug {
            assets.srcDir('build/generated/assets/sample-manager/debug/')
            assets.srcDir('build/generated/assets/plugin-zip/debug/')
        }
        release {
            assets.srcDir('build/generated/assets/sample-manager/release/')
            assets.srcDir('build/generated/assets/plugin-zip/release/')
        }
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

dependencies {
    implementation 'commons-io:commons-io:2.5'//sample-host从assets中复制插件用的
    implementation 'org.slf4j:slf4j-api:1.7.25'

    implementation 'com.tencent.shadow.core:common'
    implementation 'com.tencent.shadow.dynamic:dynamic-host'
    implementation project(':sample-constant')
    implementation project(':sample-host-lib')
}

/**
 * 打包前将 插件管理 apk 复制到当前目录中
 * 当 dependProjectName 项目的 dependTaskName 任务
 * @param dependProjectName
 * @param buildType
 * @param assetsRootPathName
 * @param outFileName
 * @param inputFile
 * @param dependTaskName
 * @return
 */
def createCopyTask(dependProjectName, buildType, assetsRootPathName, outFileName, inputFile, dependTaskName) {
    def outputFile = file("${getBuildDir()}/generated/assets/${assetsRootPathName}/${buildType}/${outFileName}")
    outputFile.getParentFile().mkdirs()
    logger.log(LogLevel.ERROR, "copy${buildType.capitalize()}${assetsRootPathName.capitalize()}Task dependsOn ${dependProjectName}:${dependTaskName}")
    return tasks.create("copy${buildType.capitalize()}${assetsRootPathName.capitalize()}Task", Copy) {
        group = 'build'
        description = "复制${assetsRootPathName}到assets中."
        from(inputFile.getParent()) {
            include(inputFile.name)
            rename { outputFile.name }
        }
        into(outputFile.getParent())

    }.dependsOn("${dependProjectName}:${dependTaskName}")
}

def generateAssets(generateAssetsTask, buildType) {

    def moduleName = 'sample-manager'
    def pluginManagerApkFile = file(
            "${project(":${moduleName}").getBuildDir()}" +
                    "/outputs/apk/${buildType}/" +
                    "${moduleName}-${buildType}.apk"
    )
    // 将 sample-manager 项目生成的 apk 文件重命名为 pluginmanager.apk 并复制到 本项目的 assets 的缓存目录下
    generateAssetsTask.dependsOn createCopyTask(
            ":${moduleName}",
            buildType,
            moduleName,
            'pluginmanager.apk',
            pluginManagerApkFile,
            "assemble${buildType.capitalize()}"
    )
    // 将根目录下的 plugin-xxx.zip 复制到 该项目的 assets/plugin-zip 缓存目录下
    def pluginZip = file("${getRootProject().getBuildDir()}/plugin-${buildType}.zip")
    generateAssetsTask.dependsOn createCopyTask(
            ':sample-plugin-app',
            buildType,
            'plugin-zip',
            "plugin-${buildType}.zip",
            pluginZip,
            "package${buildType.capitalize()}Plugin"
    )


}

tasks.whenTaskAdded { task ->
    if (task.name == "generateDebugAssets") {
        logger.log(LogLevel.ERROR, "task generateDebugAssets added")
        generateAssets(task, 'debug')
    }
    if (task.name == "generateReleaseAssets") {
        logger.log(LogLevel.ERROR, "task generateReleaseAssets added")
        generateAssets(task, 'release')
    }
}